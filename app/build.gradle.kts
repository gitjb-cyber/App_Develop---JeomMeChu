import java.util.Properties
import java.io.File
import java.io.FileNotFoundException

fun getApiKey(key: String): String {
    val properties = Properties()
    val localPropsFile = File(rootDir, "local.properties")
    if (localPropsFile.exists()) {
        properties.load(localPropsFile.inputStream())
    } else {
        throw FileNotFoundException("local.properties ÌååÏùºÏù¥ ÏóÜÏäµÎãàÎã§.")
    }

    return properties.getProperty(key)
        ?: throw IllegalArgumentException("local.propertiesÏóê ${key} ÌÇ§Í∞Ä ÏóÜÏäµÎãàÎã§.")
}

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.ksp)
}

val versionPropsFile = rootProject.file("version.properties")
val versionProps = Properties().apply {
    if (versionPropsFile.exists()) load(versionPropsFile.inputStream())
}
// Ïù¥Î≤à ÎπåÎìúÍ∞Ä release Ïù∏ÏßÄ ÏÑ§Ï†ï Îã®Í≥ÑÏóêÏÑú ÌåêÎ≥Ñ
val isReleaseBuild = gradle.startParameter.taskNames.any { it.contains("Release", ignoreCase = true) }

// ÌòÑÏû¨ ÏΩîÎìú ÏùΩÍ∏∞
var versionCodeFromFile = (versionProps["VERSION_CODE"]?.toString()?.toInt() ?: 1)

// ÎßåÏïΩ release ÎπåÎìúÎùºÎ©¥ ÏÑ§Ï†ï Îã®Í≥ÑÏóêÏÑú Ï¶âÏãú +1 ÌïòÍ≥† ÌååÏùºÏóê Ï†ÄÏû•
if (isReleaseBuild) {
    versionCodeFromFile += 1
    versionProps["VERSION_CODE"] = versionCodeFromFile.toString()
    // .use { } Î°ú ÏïàÏ†Ñ Ï†ÄÏû• (PropertiesÎäî ISO-8859-1Î°ú Ï†ÄÏû•Îê®)
    versionPropsFile.writer().use { writer ->
        versionProps.store(writer, null)
    }
    println("üì¶ versionCode bumped for THIS release build ‚Üí $versionCodeFromFile")
}

// versionNameÏùÄ ÏàòÎèô Í¥ÄÎ¶¨(ÌååÏùº Í∑∏ÎåÄÎ°ú ÏÇ¨Ïö©)
val versionNameValue: String = versionProps["VERSION_NAME"]?.toString() ?: "1.0.0"

android {
    namespace = "com.jbandroid.jeommechu"
    compileSdk = 36

    defaultConfig {
        applicationId = "com.jbandroid.jeommechu"
        minSdk = 26
        targetSdk = 36
        versionCode = versionCodeFromFile
        versionName = versionNameValue

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        resValue("string", "KAKAO_NATIVE_APP_KEY", getApiKey("KAKAO_NATIVE_APP_KEY"))
        buildConfigField("String", "KAKAO_REST_API_KEY", getApiKey("KAKAO_REST_API_KEY"))
    }

    signingConfigs {
        create("release") {
            storeFile = file("keystore/my-release-key.jks")
            storePassword = project.properties["KEYSTORE_PASSWORD"] as String
            keyAlias = "key_Jeom"
            keyPassword = project.properties["KEY_PASSWORD"] as String
        }
    }

    buildTypes {
        release {
            isMinifyEnabled = false
            signingConfig = signingConfigs.getByName("release")
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    buildFeatures {
        compose = true
    }
}

dependencies {
    implementation(libs.gms.play.services.location)

    val composeBom = platform(libs.androidx.compose.bom.v20250601)

    // ÌîåÎû´Ìèº ÏùòÏ°¥ÏÑ± (Compose BOM)
    implementation(composeBom)

    // Jetpack Compose
    implementation(libs.ui)
    implementation(libs.ui.graphics)
    implementation(libs.ui.tooling.preview)
    implementation(libs.material3)
    implementation(libs.androidx.material)
    debugImplementation(libs.ui.tooling)
    debugImplementation(libs.ui.test.manifest)

    // Lifecycle, ViewModel
    implementation(libs.androidx.lifecycle.runtime.ktx.v290)
    implementation(libs.androidx.lifecycle.viewmodel.compose)

    // Activity + Navigation
    implementation(libs.androidx.activity.compose)
    implementation(libs.androidx.navigation.compose)
    implementation(libs.androidx.navigation.runtime.ktx)

    // Room (with KSP)
    implementation(libs.androidx.room.runtime)
    implementation(libs.androidx.room.ktx)
    implementation(libs.androidx.room.common.jvm)
    ksp(libs.androidx.room.compiler)

    // Retrofit & Gson
    implementation(libs.retrofit)
    implementation(libs.converter.gson)
    implementation(libs.gson)

    // Coroutine
    implementation(libs.kotlinx.coroutines.android)
    implementation(libs.kotlinx.coroutines.play.services)

    // Google Play Services
    implementation(libs.play.services.location)
    implementation(libs.play.services.maps)

    // Accompanist (Jetpack Compose Î≥¥Ï°∞ Ïú†Ìã∏)
    implementation(libs.accompanist.systemuicontroller)
    implementation(libs.accompanist.flowlayout)

    // Í∏∞ÌÉÄ Jetpack
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.coordinatorlayout)

    // Hilt Navigation for Compose (DI ÏÇ¨Ïö© Ïãú)
    implementation(libs.androidx.hilt.navigation.compose)

    // ÌÖåÏä§Ìä∏ Í¥ÄÎ†®
    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
    androidTestImplementation(composeBom)
    androidTestImplementation(libs.ui.test.junit4)
}